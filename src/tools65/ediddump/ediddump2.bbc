    1REM SCAN HDMI I2C BUS FOR EDID INFO
    2MODE0
    3F%=OPENOUT"EDIDBIN"
   10?&EE=&D1:?&FCFF=&D1:?&FCFE=&FE:?&FCFD=&FB
   20 PROCDUMPPAGE
  150FOR P%=1 TO &7F:PRINT "PAGE = ";STR$P%
  200?&FDD1=&60:?&FDD0=&82:S%=FNBUSYWT:IF (S%AND&40)<>0THEN PRINT "CAN'T WRITE DEVICE &30":END
  210?&FDD1=P%:?&FDD0=&80:S%=FNBUSYWT
  220 PROCDUMPPAGE
  230 NEXT
  500CLOSE#F%
  999END
 1000DEFFNBUSYWT:LOCALS%:REPEAT:S%=?&FDD0:UNTIL (S% AND &80)=0:=S%
 1010:
 1020DEFPROCHEX(A%):LOCAL S$:S$=STR$~A%:PRINT MID$("  ",1,2-LEN(S$));S$;:ENDPROC   
 1030:
 1040DEFPROCDUMPPAGE
 1050?&FDD1=&A0:?&FDD0=&82:S%=FNBUSYWT:IF (S%AND&40)<>0THEN PRINT "CAN'T READ DEVICE &A0":
 1060?&FDD1=0:?&FDD0=&80:A%=FNBUSYWT:?&FDD1=&A1:?&FDD0=&82:A%=FNBUSYWT
 1070 FOR I%=0TO 255
 1080IF (I% AND &F)=0 THEN PRINT:PROCHEX(I%):PRINT" : ";  
 1090IF I%=255 THEN ?&FDD0=&85 ELSE ?&FDD0=&81
 1095S%=FNBUSYWT
 1100BPUT#F%,?&FDD1
 1110PROCHEX(?&FDD1):VDU32
 1120NEXT  
 1130ENDPROC
